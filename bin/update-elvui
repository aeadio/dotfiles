#!/usr/bin/env zsh

addonpaths=( /Applications/"World of Warcraft"/*/Interface/AddOns/(N) )

progname=${0:t}
fail() {
  print -f '%s: %s\n' $progname $1 >&2
  exit 1
}

if (( ! $#addonpaths )); then
  fail "could not locate any WoW installation directories"
fi

tmpdir=$(mktemp -d)
if [[ ! -d $tmpdir ]]; then
  fail "could not create temp directory"
fi

curl https://api.tukui.org/v1/addon/elvui | read relinfo
if [[ -z $relinfo ]]; then
  fail "could not fetch ElvUI release information"
fi

print -r $relinfo | jq -r '[.version, .url] | @tsv' | read -r version url
if [[ -z $url ]]; then
  fail "could not fetch ElvUI release URL"
fi

if [[ -n $version ]]; then
  out="elvui-$version.zip"
else
  out="elvui.zip"
fi

if ! curl -o $tmpdir/$out $url; then
  fail "could not download latest ElvUI release"
fi

for p in $addonpaths; do
  7z x -y -o"$p" $tmpdir/$out
done
