# vim: ft=sh ts=2 sw=2 et:

# Informs apps as to the directory hierarchy of ~

() {
  local gopath=(
    $HOME/[Ss]ource/go(N)
    $HOME/src/go(N)
  )
  typeset -gx GOPATH=${(j,:,)gopath}
}

typeset -g path=(
  # Scripts and personal projects
  $HOME/[Pp]rojects/*/[Bb]in(N)
  $HOME/[Bb]in(N)
  ${~${GOPATH:+${(@s,:,)GOPATH}}/%//bin}(N)  # expand multiple gopath entries correctly, with /bin appended to each
  
  # System-wide, manually managed
  /opt/bin(N)
  /opt/*/bin(N)
  
  # System-wide, owned by system package manager
  /usr/local/(|s)bin(N)
  /usr/(|s)bin(N)
  /(|s)bin(N)
  $path
)
typeset -g path=( ${(u)path} )  # (u) removes dups inherited from default $path

: ${XDG_CONFIG_HOME:=${ZSH:h}}  # :h = the enclosing folder of $ZSH
typeset -gx XDG_CONFIG_HOME

typeset -gx XDG_CACHE_HOME=$HOME/var/cache
typeset -gx XDG_DATA_HOME=$HOME/var

typeset -gx WINEPREFIX=$XDG_DATA_HOME/wine
