# vim: ft=sh ts=2 sw=2 et:

# Add/remove doas/sudo at the beginning of the line editor
# If the line editor is empty, invoke a root shell

emulate -L zsh -o extended_glob

zle || exit 1

local sucmd=${${+commands[doas]:+doas}:-sudo}

if [[ -z $BUFFER || $BUFFER == [[:space:]]# ]]; then
  if (( UID != 0 )); then
    BUFFER="$sucmd -s"
    zle accept-line
  fi
  return
fi

local firstword=${${(z)BUFFER}[1]}
local newbuffer newcursor
integer diff

if [[ $firstword == (doas|sudo) ]]; then
  newbuffer=${BUFFER/#${firstword}[[:space:]]#/}
  newcursor=$(( CURSOR - (${#BUFFER} - ${#newbuffer}) ))
else
  newbuffer="$sucmd $BUFFER"
  newcursor=$(( CURSOR + (${#newbuffer} - ${#BUFFER}) ))
fi

typeset -g BUFFER=$newbuffer
typeset -g CURSOR=$newcursor
