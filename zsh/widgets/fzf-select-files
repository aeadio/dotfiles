# vim: ft=zsh ts=2 sw=2 et:

emulate -L zsh -o multios -o prompt_subst

if (( ! $+commands[fzf] )); then
  return
fi

local query
if (( REGION_ACTIVE )); then
  local x y
  get-selection | read -r query
fi

local prev_opts
if (( $+commands[preview] )); then
  prev_opts=(
    --preview="preview {}"
    --preview-window=right,60%,border-left
  )
else
  prev_opts=(
    --height=40%
  )
fi

local ret
local matches=$(
  find . | fzf \
    --layout=reverse     \
    --filepath-word      \
    --tiebreak=begin     \
    --query=${query:-$1} \
    ${prev_opts}         \
    --multi
) || ret=$?
matches=( ${(f)matches} )

zle && zle reset-prompt

if (( ret || ${#matches} == 0 )); then
  return $ret
fi

if (( REGION_ACTIVE )); then
  replace-selection $matches
elif zle; then
  insert-with-space $matches
else
  print -nr -- $matches
fi
