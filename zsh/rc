# vim: ft=sh ts=2 sw=2 et:

# This file primarily bootstraps the zshrc. For actual config, see:
# - rc.d      most user configuration
# - pre.d     early-run initialization, eg compinit
# - env.d     static exports for all shells, including non-interactive
# - incl.d    non-rc config files for sub utilities
# - functions utility functions for the user, auto-loaded on demand
# - widgets   functions which modify Zsh's input editor (see also: rc.d/zle)
# - plugins   bundles of self-contained functionality

# Each section below is wrapped in a named function so that we can blame their
# contribution to startup when ZSHRC_PROFILE is set, and trace their activity
# in the log when ZSHRC_DEBUG is set.

: ${ZSHRC_PROFILE:=0}
: ${ZSHRC_DEBUG:=0}

if [[ ! -d $ZSH ]]; then
  # Exported by zsh/env
  print "Warning: zsh config directory not found: $ZSH"
  return
fi

# Tracing entrypoint
(( ZSHRC_DEBUG > 0 || ZSHRC_PROFILE )) &&
  exec 2> >(tee "$HOME/zshrc-$(date +'%Y-%m-%dT%T').log")
(( ZSHRC_DEBUG > 0 )) && setopt warn_create_global warn_nested_var
(( ZSHRC_DEBUG > 1 )) && setopt source_trace
(( ZSHRC_DEBUG > 2 )) && setopt verbose
(( ZSHRC_DEBUG > 3 )) && setopt xtrace
(( ZSHRC_PROFILE )) && zmodload zsh/zprof

# pre.d
zshrc:load-pre.d() {
  local f name REPLY
  for f in $ZSH/pre.d/*(e:'[[ -x $REPLY ]]':onN); do
    name=${f:t}
    zshrc:load-pre.d:$name() {
      source $f
    }
    zshrc:load-pre.d:$name
    unfunction zshrc:load-pre.d:$name
  done
}
zshrc:load-pre.d
unfunction zshrc:load-pre.d

# Plugins
# Plain (executable) files in zsh/plugins/ are first-party plugins -- mine.
# Directories are third-party plugins (if they contain a .plugin.zsh file).
# See zsh/functions/zp* for installation helper.
zshrc:load-plugins() {
  local f name REPLY
  local plugins=(
    $ZSH/plugins/*(-.e:'[[ -x $REPLY ]]':onN)
    $ZSH/plugins/*/*.plugin.zsh(-.onN)
  )
  for f in $plugins; do
    name=${f:t}
    zshrc:load-plugins:$name() {
      source $f
    }
    zshrc:load-plugins:$name
    unfunction zshrc:load-plugins:$name
  done
}
zshrc:load-plugins
unfunction zshrc:load-plugins

# User functions & ZLE widgets
# zsh/functions/* and zsh/widgets/* are autoloaded only if marked executable
zshrc:load-functions() {
  local f REPLY
  local fns=( $ZSH/functions/*(-.e:'[[ -x $REPLY ]]':N:t) )
  local wdg=( $ZSH/widgets/*(-.e:'[[ -x $REPLY ]]':N:t) )
  if (( ${#fns} )); then
    fpath=( $ZSH/functions $fpath )
    autoload $fns
  fi
  if (( ${#wdg} )); then
    fpath=( $ZSH/widgets $fpath )
    autoload $wdg
    for f in $wdg; zle -N $f
  fi
}
zshrc:load-functions
unfunction zshrc:load-functions

# zshrc.d
# Primary place for zsh config. Load last so that it may make use of plugins.
zshrc:load-rc.d() {
  local f name REPLY
  for f in $ZSH/rc.d/*(e:'[[ -x $REPLY ]]':onN); do
    name=${f:t}
    zshrc:load-rc.d:$name() {
      source $f
    }
    zshrc:load-rc.d:$name
    unfunction zshrc:load-rc.d:$name
  done
}
zshrc:load-rc.d
unfunction zshrc:load-rc.d

# Stop profiling
(( ZSHRC_PROFILE )) && zprof
