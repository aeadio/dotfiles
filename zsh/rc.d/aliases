#!/usr/bin/env zsh
# vim: ft=sh ts=2 sw=2 et:

# default command options
if isgnu grep; then
  alias grep='grep --color=auto'
fi
has ranger && alias ranger='. ranger'

# misc extras
has curl && alias sprunge="curl -F 'sprunge=<-' http://sprunge.us"
has git  && alias conf="env GIT_DIR=$CONF/.git GIT_WORK_TREE=$CONF git"

# tool shorthands
if isgnu ls; then
  alias l='ls -lFkh --quoting-style=literal --color=auto --group-directories-first'
  alias L='ls -lFkh --quoting-style=literal --color=auto --group-directories-first -a'
else
  alias l='ls -lpq'
  alias L='ls -lpq -a'
fi
has micro    && alias e='micro'
has ranger   && alias f='ranger'
has code-oss && alias E='code-oss'
has code     && alias E='code'

# platform specific
case $OS in
  darwin)
    # x86 aliases
    () {
      setopt glob_subst
      local isa=$(uname -m)
      local emu
      if [[ $isa == 'arm64' ]]; then
        emu="arch --x86_64"
        # If we're on M1 Mac and x86 Homebrew is installed, alias to 'briw'.
        [[ -x /usr/local/Homebrew/bin/brew ]] &&
          alias briw="$emu /usr/local/Homebrew/bin/brew"
      fi
      # The following searches for all Terraform versions installed via 
      # Homebrew. Sets up an alias for each version in the form 'terraformXX' 
      # where XX is the minor number for 0.XX releases. Emulation via Rosetta 
      # is used, if needed.
      local tf ver bin
      for tf in /usr/local/Cellar/terraform@*(N) /opt/homebrew/Cellar/terraform@*(N); do
        ver=${${${tf:t}#*@}#0.}
        bin=( $tf/*/bin/terraform(NOn) )
        [[ -n $ver && -n ${bin[1]} ]] || continue
        if quiet file ${bin[1]} | grep -q $isa; then
          alias "terraform$ver"="${bin[1]}"
        else
          alias "terraform$ver"="$emu ${bin[1]}"
        fi
      done
      setopt local_options
    }
    ;;
  linux)
    # Void tools
    if has xbps-query; then
      alias xr='xbps-remove -R'
      alias xoo='xbps-remove -oO'
      alias xqf='xbps-query -f'
      alias xqx='xbps-query -x'
      alias XQX='xbps-query -X'
    fi
    if has vsv; then
      alias s="vsv -d $HOME/sv"
      alias S='vsv'
    fi
    has vkpurge && alias vkp='vkpurge rm all'
    ;;
esac

case $GUI in
  aqua)
    alias c='pbcopy'
    alias p='pbpaste'
    ;;
  x11)
    if has xsel; then
      alias c='xsel -ib'
      alias p='xsel -ob'
    elif has xclip; then
      alias c='xclip -in -selection clipboard'
      alias p='xclip -out -selection clipboard'
    fi
    ;;
esac
