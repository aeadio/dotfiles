#!/usr/bin/env zsh
# vim: ft=sh ts=2 sw=2 et:

# default command options
if isgnu grep; then
  alias grep='grep --color=auto'
fi
has ranger && alias ranger='. ranger'

# misc extras
has curl && alias sprunge="curl -F 'sprunge=<-' http://sprunge.us"
has git  && alias conf="env GIT_DIR=$CONF/.git GIT_WORK_TREE=$CONF git"

# tool shorthands
if isgnu ls; then
  alias l='ls -lFkh --quoting-style=literal --color=auto --group-directories-first'
  alias L='ls -lFkh --quoting-style=literal --color=auto --group-directories-first -a'
else
  alias l='ls -lpq'
  alias L='ls -lpq -a'
fi
has micro    && alias e='micro'
has ranger   && alias f='ranger'
has code-oss && alias E='code-oss'
has code     && alias E='code'

# platform specific
case $OS in
  darwin)
    # x86 aliases
    () {
      emulate -L zsh
      if [[ $(arch) == 'arm64' ]]; then
        [[ -x /usr/local/Homebrew/bin/brew ]] &&
          alias briw='/usr/local/Homebrew/bin/brew'
      fi
      # Search for all alternative/pinned versions of Homebrew packages, and
      # sets up <name><ver> aliases for each of that package's binaries.
      local brew_prefixes=( /usr/local/Cellar*(N) /opt/homebrew/Cellar*(N) )
      local prefix progpath ver binpaths bin
      for prefix in $brew_prefixes; do
        for progpath in $prefix/*@*(-/N); do
          ver=${${progpath:t}##*@}
          binpaths=( $progpath/*/bin(-/OnN) )
          [[ -n $ver && -n $binpaths[1] ]] || continue
          for bin in $binpaths[1]/*(-*N);
            alias "${bin:t}$ver"=$bin
        done
      done
    }
    ;;
  linux)
    # Void tools
    if has xbps-query; then
      alias xr='xbps-remove -R'
      alias xoo='xbps-remove -oO'
      alias xqf='xbps-query -f'
      alias xqx='xbps-query -x'
      alias XQX='xbps-query -X'
    fi
    if has vsv; then
      alias s="vsv -d $HOME/sv"
      alias S='vsv'
    fi
    has vkpurge && alias vkp='vkpurge rm all'
    ;;
esac

case $GUI in
  aqua)
    alias c='pbcopy'
    alias p='pbpaste'
    ;;
  x11)
    if has xsel; then
      alias c='xsel -ib'
      alias p='xsel -ob'
    elif has xclip; then
      alias c='xclip -in -selection clipboard'
      alias p='xclip -out -selection clipboard'
    fi
    ;;
esac
