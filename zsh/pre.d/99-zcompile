# vim: ft=sh ts=2 sw=2 et:

# Automatically zcompile all sources regularly. While this has a miniscule 
# effect on simple files (ie, most of rc.d -- within the margin of error for
# timing), it can help out a lot on more heavyweight sources in third party
# plugins, or on zcompcache.

() {
  setopt localoptions extended_glob
  (
    # Run async in a subshell
    # Zsh validates that a compiled version of a source file is valid and 
    # matches the original source file, so it's safe to let the shell 
    # potentially race zcompile when attempting to source something.
    local f REPLY
    for f in {$ZSH,${XDG_DATA_HOME:-$HOME/.local/share}/zsh}/(*/)#(*.(z|)sh|^*.*)(-.e:'[[ ! -e $REPLY.zwc || $REPLY.zwc -ot $REPLY ]]':N);
      zcompile $f &>/dev/null
  ) &|
}
