# vim: ft=zsh ts=2 sw=2 et:

emulate -L zsh

if (( ! $# )); then
  print "usage: awsprofile profilename" >&2
  return
fi

if [[ $1 == $AWS_PROFILE ]]; then
  return
fi

local profname
sed 's/[.[\*^()$+?{|]/\\&/g' <<< $1 | read -r profname

local conf=${XDG_CONFIG_HOME:-$HOME/.config}

[[ $(cat $conf/aws/credentials) =~ \[$profname\] ]] ||
[[ $(cat $conf/aws/config)      =~ \[$profname\] ]] ||
{
  print "awsprofile: profile $1 not found in stored credentials" >&2
  return
}

typeset -gx AWS_PROFILE=$1
unset AWS_ACCESS_KEY_ID AWS_SECRET_ACCESS_KEY AWS_SESSION_TOKEN

if (( $+functions[dynexport] )); then
  dynexport AWS_PROFILE
  dynunset AWS_ACCESS_KEY_ID AWS_SECRET_ACCESS_KEY AWS_SESSION_TOKEN
fi
