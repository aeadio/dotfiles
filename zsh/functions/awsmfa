# vim: ft=zsh ts=2 sw=2 et:

emulate -L zsh

if [[ -z $MFA_ARN ]]; then
  print "$0: MFA_ARN not set" >&2
  return 1
fi

local mfacode

if (( ! $# )); then
  print -n "MFA code: " >&2
  read mfacode
else
  mfacode=$1
fi

unset AWS_ACCESS_KEY_ID
unset AWS_SECRET_ACCESS_KEY
unset AWS_SESSION_TOKEN

local key

aws sts get-session-token \
  --duration-seconds ${MFA_DURATION:-86400} \
  --output json \
  --serial-number $MFA_ARN \
  --token-code $mfacode |
  jq -r '.Credentials | [.AccessKeyId, .SecretAccessKey, .SessionToken] | @tsv'
  | read -A key

if [[ -z $key[1] ]]; then
  print "$0: failed to get credentials" >&2
  return 2
fi

typeset -gx AWS_ACCESS_KEY_ID=$key[1]
typeset -gx AWS_SECRET_ACCESS_KEY=$key[2]
typeset -gx AWS_SESSION_TOKEN=$key[3]

if (( $+functions[dynexport] )); then
  dynexport AWS_ACCESS_KEY_ID AWS_SECRET_ACCESS_KEY AWS_SESSION_TOKEN
fi
