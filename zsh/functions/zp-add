#!/usr/bin/env zsh
# vim: ft=sh ts=2 sw=2 et:
emulate -L zsh

if (( ! $# )); then
  >&2 <<< "usage: zp-add <giturl> [<giturl>...]"
  return
fi

if (( ! $+commands[git] )); then
  >&2 <<< "fatal: requires git"
  return
fi

if [[ ! -d $ZSH ]]; then
  >&2 <<< "error: zsh config directory not found: $ZSH"
  return
fi

local plugindir=$ZSH/plugins
mkdir -p $plugindir

local repo name
for repo in "$@"; do
  [[ -z "$repo" ]] && continue
  name=${repo:t:r}
  if [[ -e $plugindir/$name ]]; then
    >&2 <<< "warning: plugin $name exists, skipping..."
    continue
  fi
  (
    if ! cd $plugindir; then
      >&2 <<< "error: Could not enter plugin directory."
      return 1
    fi
    git clone $repo $name
  )
  if (( $? )); then
    >&2 <<< "error: plugin $name failed to install"
    return
  fi
done
