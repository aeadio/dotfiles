# vim: ft=sh ts=2 sw=2 et:

() {
  case $OSTYPE in
    linux*|*bsd*)
      # Start a graphical session if we're on a configured TTY
      if [[ $TTY != /dev/(tty1|ttyv0) ]] return
      
      # TODO: Interface with seat manager and support multiple seats?
      pgrep sway  &> /dev/null && return
      pgrep xinit &> /dev/null && return
      
      local targets=()
      if (( $+commands[startx] )) targets+=(startx)
      if (( $+commands[sway] ))   targets+=(sway)
      
      if (( $#targets == 1 )); then
        exec $targets[1]
      elif (( $#targets > 1 )); then
        print "Choose a session:"
        select target in $targets; do
          exec $target
          break
        done
      fi
      ;;
  esac
}
