# vim: ft=sh ts=2 sw=2 et:

# Attempts to track the size of the bufstack (the ZLE stack used during
# push-line and get-line). May not cover some edge cases if plugins are
# manipulating the buffer and stack.

_bufstack_push-input() {
  (( BUFSTACK++ ))
  zle .push-input
}
zle -N push-input _bufstack_push-input

_bufstack_push-line() {
  (( BUFSTACK++ ))
  zle .push-line
}
zle -N push-line _bufstack_push-line

_bufstack_push-line-or-edit() {
  if [[ $CONTEXT == start ]]; then
    (( BUFSTACK++ ))
  fi
  zle .push-line-or-edit
}
zle -N push-line-or-edit _bufstack_push-line-or-edit

_bufstack_get-line() {
  (( BUFSTACK > 0 && BUFSTACK-- ))
  zle .get-line
}
zle -N get-line _bufstack_get-line

_bufstack_line-init() {
  (( BUFSTACK > 0 && BUFSTACK-- ))
}
autoload -Uz add-zsh-hook
add-zsh-hook precmd _bufstack_line-init
#autoload -Uz add-zle-hook-widget &&
#add-zle-hook-widget zle-line-init _bufstack_line-init
