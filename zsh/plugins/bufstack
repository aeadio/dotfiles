# vim: ft=sh ts=2 sw=2 et:

# Attempts to track the size of the bufstack (the ZLE stack used during
# push-line and get-line). May not cover some edge cases if plugins are
# manipulating the buffer and stack.

bufstack:push-input() {
  emulate -L zsh
  typeset -g BUFSTACK
  (( BUFSTACK++ ))
  zle .push-input
}
zle -N push-input bufstack:push-input

bufstack:push-line() {
  emulate -L zsh
  typeset -g BUFSTACK
  (( BUFSTACK++ ))
  zle .push-line
}
zle -N push-line bufstack:push-line

bufstack:push-line-or-edit() {
  emulate -L zsh
  typeset -g BUFSTACK
  if [[ $CONTEXT == start ]]; then
    (( BUFSTACK++ ))
  fi
  zle .push-line-or-edit
}
zle -N push-line-or-edit bufstack:push-line-or-edit

bufstack:get-line() {
  emulate -L zsh
  typeset -g BUFSTACK
  (( BUFSTACK > 0 && BUFSTACK-- ))
  zle .get-line
}
zle -N get-line _bufstack_get-line

bufstack:line-init() {
  emulate -L zsh
  typeset -g BUFSTACK
  (( BUFSTACK > 0 && BUFSTACK-- ))
}
autoload -Uz add-zsh-hook
add-zsh-hook precmd bufstack:line-init
