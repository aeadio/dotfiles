# vim: ft=sh ts=2 sw=2 et:

bad-input:reset() {
  emulate -L zsh
  typeset -g _BAD_INPUT
  (( --_BAD_INPUT )) || unset _BAD_INPUT
}
autoload -Uz add-zsh-hook
add-zsh-hook precmd bad-input:reset

typeset -Ag PROMPT_DANGEROUS=(
  '>' 1
  '$' 1
  "${(#):-16#226f}" 1
  "${(#):-16#bb}"   1
  "${(#):-16#203a}" 1
)

# Note: this widget is auto-invoked by ZLE if present
bad-input:zle-line-finish() {
  emulate -L zsh -o extended_glob
  local firstword=${${(z)BUFFER}[1]}
  if (( $+PROMPT_DANGEROUS[$firstword] )); then
    BUFFER=${BUFFER/#${firstword}[[:space:]]#/}
    zle push-line
    BUFFER=
    typeset -g _BAD_INPUT=2
  fi
}
autoload -Uz add-zle-hook-widget &&
add-zle-hook-widget zle-line-finish bad-input:zle-line-finish
