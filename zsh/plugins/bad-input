# vim: ft=sh ts=2 sw=2 et:

# If the user copy-pasted a code snippet beginning with some caret character,
# then prevents them from trying to execute it with that (potentially
# dangerous) caret in-place. Instead, loads the line, caret removed, back into
# the line editor when they try to execute it, setting _BAD_INPUT so the prompt
# can display some visual warning cue.

bad-input:reset() {
  emulate -L zsh
  typeset -g _BAD_INPUT
  (( --_BAD_INPUT )) || unset _BAD_INPUT
}
autoload -Uz add-zsh-hook
add-zsh-hook precmd bad-input:reset

typeset -Ag PROMPT_DANGEROUS=(
  '>' 1
  '$' 1
  "${(#):-16#226f}" 1
  "${(#):-16#bb}"   1
  "${(#):-16#203a}" 1
)

# Note: this widget is auto-invoked by ZLE if present
bad-input:zle-line-finish() {
  local firstword
  [[ $options[interactivecomments] == on ]] &&
    firstword=${${(Z+C+)BUFFER}[1]} ||
    firstword=${${(z)BUFFER}[1]}
  if (( $+PROMPT_DANGEROUS[$firstword] )); then
    setopt localoptions extended_glob
    typeset -g BUFFER=${BUFFER/#${firstword}[[:space:]]#/}
    zle push-line
    typeset -g BUFFER=
    typeset -g _BAD_INPUT=2
  fi
}
autoload -Uz add-zle-hook-widget &&
add-zle-hook-widget zle-line-finish bad-input:zle-line-finish
